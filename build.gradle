/*****************************************************************************
 * Project:  AndroidLogcatStdouts
 * Purpose:  Library for the redirection a stdouts
 *           from the native code to the Adroid's logcat.
 * Author:   NikitaFeodonit, nfeodonit@yandex.com
 *****************************************************************************
 *   Copyright (c) 2017-2018 NikitaFeodonit
 *
 *    This file is part of the AndroidLogcatStdouts project.
 *
 *    This program is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published
 *    by the Free Software Foundation, either version 3 of the License,
 *    or (at your option) any later version.
 *
 *    This program is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 *    See the GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with this program. If not, see <http://www.gnu.org/licenses/>.
 ****************************************************************************/

apply plugin: 'com.android.library'

//-----------------------------------------------------------------------
// Common vars from root project
//-----------------------------------------------------------------------

def ignoreVariantBuildType = ""
if (rootProject.hasProperty("ignoreVariantBuildType")) {
  ignoreVariantBuildType = rootProject.ignoreVariantBuildType
}

def buildToolsVer = "27.0.3"
if (rootProject.hasProperty("buildToolsVersion")) {
  buildToolsVer = rootProject.buildToolsVersion
}

def compileSdkVer = 26
if (rootProject.hasProperty("compileSdkVersion")) {
  compileSdkVer = rootProject.compileSdkVersion
}

def targetSdkVer = 26
if (rootProject.hasProperty("targetSdkVersion")) {
  targetSdkVer = rootProject.targetSdkVersion
}

def minSdkVer = 16
if (rootProject.hasProperty("minSdkVersion")) {
  minSdkVer = rootProject.minSdkVersion
}

def supportLibVer = "26.1.0"
if (rootProject.hasProperty("supportLibVersion")) {
  supportLibVer = rootProject.supportLibVersion
}

//def abiFilts = ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
def abiFilts = ["x86"]
if (rootProject.hasProperty("abiFilters")) {
  abiFilts = rootProject.abiFilters
}

def rootBuildDir = "${rootDir}/build_android/${rootProject.name}"
if (rootProject.hasProperty("rootBuildDir")) {
  rootBuildDir = rootProject.rootBuildDir
}

def rootNativeBuildDir = "${rootBuildDir}/.nativeBuild"
if (rootProject.hasProperty("rootNativeBuildDir")) {
  rootNativeBuildDir = rootProject.rootNativeBuildDir
}

def BUILD_SHARED_LIBS = false
if (rootProject.hasProperty("BUILD_SHARED_LIBS")) {
  BUILD_SHARED_LIBS = rootProject.BUILD_SHARED_LIBS
}

//-----------------------------------------------------------------------
// Module properties
//-----------------------------------------------------------------------

// https://stackoverflow.com/a/32129704
//import com.android.builder.core.BuilderConstants
android.variantFilter { variant ->
//  if (build == BuilderConstants.RELEASE) {
  if (variant.buildType.name == ignoreVariantBuildType) {
    variant.setIgnore(true)
  }
}

//-----------------------------------------------------------------------
// Android Java part
//-----------------------------------------------------------------------

android {
  compileSdkVersion compileSdkVer
  buildToolsVersion buildToolsVer

  defaultConfig {
    minSdkVersion minSdkVer
    targetSdkVersion targetSdkVer
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      debuggable false
      jniDebuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField("Boolean", "BUILD_SHARED_LIBS", "${BUILD_SHARED_LIBS}")
    }
    debug {
      debuggable true
      jniDebuggable true

      buildConfigField("Boolean", "BUILD_SHARED_LIBS", "${BUILD_SHARED_LIBS}")
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//-----------------------------------------------------------------------
// Native CMake part
//-----------------------------------------------------------------------

// useful links
// https://developer.android.com/studio/projects/add-native-code.html
// https://developer.android.com/ndk/guides/cmake.html#variables
// https://developer.android.com/ndk/guides/abis.html#sa

// Waiting for fix: https://code.google.com/p/android/issues/detail?id=52962

android {
// TODO: set from parent project
//  defaultPublishConfig "debug" // uncomment it for the debug of the native code
}

android {
// From Android Studio 2.2 Preview 6 (July 22, 2016) report:
// significantly improve build/deploy/debug cycle by only building the ABI
// that is needed for the target device or emulator.

//def cmAndroidAbis = ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
//def String abiFiltersString = cmAndroidAbis.toString()
//abiFiltersString = abiFiltersString.substring(1, abiFiltersString.length() - 1)

  def projectNativeBuildDir = "${rootNativeBuildDir}/${project.name}"

  defaultConfig {
    ndk {
      abiFilters = abiFilts // what ABIs gradle should package into one apk
    }

    externalNativeBuild {
      cmake {
        abiFilters = abiFilts // what ABIs gradle should build

        def String androidStl = "c++_static"
        if (BUILD_SHARED_LIBS) {
          androidStl = "c++_shared"
        }

//        targets "cmaketarget1", "cmaketarget2", "cmaketarget3" // TODO:

        // STUB_NOT_USED_CMAKE_VAR is a stub to simply turn off
        // the underlying variables with commenting.
        arguments "-DSTUB_NOT_USED_CMAKE_VAR=OFF",

            "-DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}",

//            "-DCMAKE_VERBOSE_MAKEFILE=TRUE",

//            "-DANDROID_PLATFORM=android-9", // ${getNdkPlatformLevel(abi)}
// We could not specify ANDROID_PLATFORM:
// From Android Studio 2.2 Preview 5 (July 8, 2016) report:
// Enhanced logic for selecting platform version for ndk-build and CMake:
//  (1) get the set of ABIs based on compileSdkVersion.
//  (2) then for each of these ABIs we have decided to build for,
//      (a) if platforms/android-[min sdk]/arch-[ABI] exists,
//          then use the min sdk  as platform for that ABI.
//      (b) else if there exists platforms/android-[platform]/arch-[ABI] such that
//          platform < min sdk, use max(platform where platform < min sdk).
//      (c) else use min(platform where platforms/android-[platform]/arch-[ABI]  exists).

//            "-DANDROID_CPP_FEATURES=rtti exceptions",
//            "-DCMAKE_TOOLCHAIN_FILE=${projectDir}/...../android.toolchain.cmake",
//            "-GAndroid Gradle - Unix Makefiles",
//            "-DCMAKE_MAKE_PROGRAM=make",

//            "-DCXX_STANDARD=14",
//            "-DCXX_STANDARD_REQUIRED=ON",

//            "-DCMAKE_BUILD_TYPE=Release", // let's always release ${buildTypeName}

            // gcc
//            "-DANDROID_TOOLCHAIN=gcc",
//            "-DANDROID_STL=gnustl_static",

            // clang
            "-DANDROID_TOOLCHAIN=clang",
            "-DANDROID_STL=${androidStl}",

            "-DSTUB_NOT_USED_CMAKE_VAR=OFF"
      }
    }
  }
  externalNativeBuild {
    cmake {
      path = "${projectDir}/src/main/cpp/CMakeLists.txt"
      // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.CmakeOptions.html
      buildStagingDirectory = "${projectNativeBuildDir}"
    }
  }

  // To combine with others native modules which have the c++_shared
  // to prevent the error:
  // "More than one file was found with OS independent path 'lib/<abi>/libc++_shared.so'"
  if (BUILD_SHARED_LIBS && rootProject.hasProperty("excludeSharedStl") &&
      rootProject.excludeSharedStl) {
    abiFilts.each { abiFilt ->
      packagingOptions {
        exclude "lib/${abiFilt}/libc++_shared.so"
      }
    }
  }

// TODO:
//  androidAbis.each { androidAbi ->
//    task "cleanNative-${androidAbi}"(type: Exec) {
//      workingDir getWorkDir(androidAbi)
//      def cmakeCmd = "cmake --build . --target clean"
//      commandLine getShell(), getShellArg(), "${cmakeCmd}"
//    }
//  }
//  clean.dependsOn androidAbis.collect {
//    androidAbi -> "cleanNative-${androidAbi}"
//  }
}

//-----------------------------------------------------------------------
// Debug printing
//-----------------------------------------------------------------------

if (rootProject.hasProperty("buildGradleDebug") &&
    rootProject.buildGradleDebug) {
  println "================================================================"
  println "Properties of module '${project.name}':"
  println "================================================================"
  println "ignoreVariantBuildType == ${ignoreVariantBuildType}"
  println "android.buildToolsVersion == ${android.buildToolsVersion}"
  println "android.compileSdkVersion == ${android.compileSdkVersion}"
  println "android.defaultConfig.targetSdkVersion == ${android.defaultConfig.targetSdkVersion}"
  println "android.defaultConfig.minSdkVersion == ${android.defaultConfig.minSdkVersion}"
  println "supportLibVer == ${supportLibVer}"
  println "android.defaultConfig.ndk.abiFilters == ${android.defaultConfig.ndk.abiFilters}"
  println "android.defaultConfig.externalNativeBuild.cmake.abiFilters == ${android.defaultConfig.externalNativeBuild.cmake.abiFilters}"
  println "rootBuildDir == ${rootBuildDir}"
  println "rootProject.allprojects.buildDir == ${rootProject.allprojects.buildDir}"
  println "project.buildDir == ${project.buildDir}"
  println "android.externalNativeBuild.cmake.buildStagingDirectory == ${android.externalNativeBuild.cmake.buildStagingDirectory}"
  println "================================================================"
}
